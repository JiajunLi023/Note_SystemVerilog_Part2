```
// Code your testbench here
// or browse Examples
`timescale 1ns / 1ps

class transaction;
  rand bit din;
  bit dout;
  
  function transaction copy();
    copy = new();
    copy.din = this.din;
    copy.dout = this.dout;
  endfunction
  
  function void display(input string tag);
    $display("[%0s]: din : %0b\t dout : %0b\t", tag, din, dout);
  endfunction
  
endclass

////////////////////////

class generator;
  transaction ds;
  mailbox #(transaction) mbx;
  int count = 0;
  event next;
  event done;
  
  function new(mailbox #(transaction) mbx);
    ds = new();
    this.mbx = mbx;
  endfunction
  
  task run();
    repeat(count) begin
      assert(ds.randomize()) else $error("Randomization Failed.");
      mbx.put(ds.copy());
      ds.display("GEN");
      @(next);
    end
    -> done;
  endtask
endclass

////////////////////////

class driver;
  transaction dr;
  mailbox #(transaction) mbx;
  virtual dff_if dif;
  
  function new(mailbox #(transaction) mbx);
    this.mbx = mbx;
  endfunction
  
  task reset();
    dif.rst <= 1'b1;
    repeat(5) @(posedge dif.clk);
    $display("RESET OVER");
    dif.rst <= 1'b0;
  endtask
  
  task run();
    forever begin
      mbx.get(dr);
      dr.display("DRV");
      dif.din <= dr.din;
      @(posedge dif.clk);
    end
  endtask
endclass

///////////////////////

class monitor;
  transaction ds;
  mailbox #(transaction) mbx;
  virtual dff_if dif;
  
  function new(mailbox #(transaction) mbx);
    this.mbx = mbx;
  endfunction
  
  task run();
    ds = new();
    
    forever begin
      repeat(2) @(posedge dif.clk);
      ds.din = dif.din;
      ds.dout = dif.dout;
      mbx.put(ds);
      ds.display("MON");
    end
  endtask
  
endclass

//////////////////////

class scoreboard;
  transaction dr;
  mailbox #(transaction) mbx;
  event next;
  
  function new(mailbox #(transaction) mbx);
    this.mbx = mbx;
  endfunction
  
  task run();
    forever begin
      mbx.get(dr);
      dr.display("SCO");
      
      if(dr.din == dr.dout) begin
        $display("DATA MATCH");
      end
      else begin
        $display("DATA MISMATCH");
      end
      -> next;
    end
  endtask
endclass

///////////////////////

class environment;
  generator gen;
  driver drv;
  monitor mon;
  scoreboard sco;
  event next;
  mailbox #(transaction) mbxgd;
  mailbox #(transaction) mbxms;
  virtual dff_if dif;
  
  
  function new(virtual dff_if dif);
    mbxgd = new();
    gen = new(mbxgd);
    drv = new(mbxgd);
    
    mbxms = new();
    mon = new(mbxms);
    sco = new(mbxms);
    
    this.dif = dif;
    drv.dif = this.dif;
    mon.dif = this.dif;
    
    gen.next = next;
    sco.next = next;
  endfunction
  
  task pre_test();
    drv.reset();
  endtask
  
  task test();
    fork
      gen.run();
      drv.run();
      mon.run();
      sco.run();
    join_any
  endtask
  
  task post_test();
    wait(gen.done.triggered);
    $finish();
  endtask
  
  task run();
    pre_test();
    test();
    post_test();
  endtask
  
endclass

///////////////////////////

module tb();
  dff_if dif();
  dff dut(dif);
  
  initial begin
    dif.clk <= 0;
  end
  
  always #10 dif.clk <= ~dif.clk;
  
  environment env;
  
  initial begin
    env = new(dif);
    env.gen.count = 20;
    env.run();
  end 
  
  initial begin 
    $dumpfile("dump.vcd");
    $dumpvars;
  end
endmodule
```
